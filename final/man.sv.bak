module man (	
					input logic				Clk,             	// 50 MHz clock
												Reset,         	// Active-high reset signal
												frame_clk,        // The clock indicating a new frame (~60Hz)
					input logic  [1:0]	move,					
					input logic  [3:0]	barrier,
					input logic				jump,
					output logic [9:0] 	man_x,
												man_y,
					output logic [1:0]	man_state,			// 00 -> still, 01 -> x-move, 10 -> jump, 11 -> fall
												man_graph_counter
);
				  
				  
				  
		logic [9:0] man_x_next, man_y_next;
		logic [9:0] motion_x, motion_y;
		logic [2:0]	jump_state, jump_state_next;
		logic [1:0] man_state_next, man_graph_counter_next;
		logic change_graph;
	 
	 
	 
		// find the rising edge of the frame clk
		logic frame_clk_delayed, frame_clk_rising_edge;
		always_ff @ (posedge Clk) begin
			frame_clk_delayed <= frame_clk;
			frame_clk_rising_edge <= (frame_clk == 1'b1) && (frame_clk_delayed == 1'b0);
		end

	 
	 
		// the registers which hold the position and motion status of the man
		always_ff @ (posedge Clk)
		begin
			if (Reset) begin
				man_x <= 10'd30;
				man_y <= 10'd30;
				jump_state <= 3'b0;
				man_state <= 2'b0; 
				man_graph_counter <= 2'b0;
			end
			else begin
				man_x <= man_x_next;
				man_y <= man_y_next;
				jump_state <= jump_state_next;
				man_state <= man_state_next;
				man_graph_counter <= man_graph_counter_next;
			end
		end
		
		
		
		// the combinational logic of position and movement
		// By default, keep motion and position unchanged
		
		// x-motion
		always_comb
		begin
			man_x_next = man_x;
			motion_x = 10'b0;
			
			// Update position and motion only at rising edge of frame clock
			if (frame_clk_rising_edge)	begin
				// check whether x-movement is legal
				unique case (move)
					2'b00:;
					2'b10: begin
							if (barrier[1] == 1)
								motion_x = ~(10'b1) + 10'b1;
						end
					2'b01: begin
							if (barrier[0] == 1)
								motion_x = 10'b1;
						end
					default:;
				endcase
			end
			
			// Update the man's position with its motion
			man_x_next = man_x + motion_x;
		end
		
		// y-motion
		always_comb
		begin
			man_y_next = man_y;
			motion_y = 10'b0;
			jump_state_next = jump_state;
			
			// Update position and motion only at rising edge of frame clock
			if (frame_clk_rising_edge)	begin
				// check whether y-movement is legal
				if (jump_state == 3'b000) begin	// all zero represents not jumping --> still or falling
					if (barrier[3] == 1'b1)
						motion_y = 10'b1;
				end
				else if (jump_state != 3'b111) begin
					if (barrier[2] == 1'b1)
						motion_y = ~(10'd6) + 10'b1;
				end
				else begin
					if (barrier[3] == 1'b1)
						motion_y = 10'd4;
				end
				
				// update jump_state
				if (jump_state == 3'b000) begin
					if (jump == 1'b1)
						jump_state_next = 3'b001;
				end
				else if (jump_state != 3'b111)
					jump_state_next = jump_state + 3'b1;
				else begin
					if (barrier[3] == 1'b0)
						jump_state_next = 3'b000;
				end
			end
			
			// Update the man's position with its motion
			man_y_next = man_y + motion_y;
		end
		
		
		
		// man_state and man_graph_counter
		always_comb
		begin
			man_state_next = man_state;
			
			// Update position and motion only at rising edge of frame clock
			if (frame_clk_rising_edge)	begin
				if (jump_state_next == 3'b000) begin
					if (motion_x == 10'b0)
						man_state_next = 2'b00;
					else 
						man_state_next = 2'b01;
				end
				else if (jump_state_next == 3'b111)
					man_state_next = 2'b11;
				else 
					man_state_next = 2'b10;
			end
		end
		
		assign change_graph = (man_state != man_state_next);
		
		always_comb
		begin
			man_graph_counter_next = man_graph_counter;
			
			if (frame_clk_rising_edge)	begin
				if (change_graph)
					man_graph_counter_next = 2'b0;
				else if (man_graph_counter == 2'b11)
					man_graph_counter_next = 2'b0;
				else
					man_graph_counter_next = man_graph_counter + 2'b1;
			end
		end
		
endmodule
